Program.Sub.ScreenSU.Start
gui.Form1..create
gui.Form1..caption("Select Location for File Export")
gui.Form1..size(5805,2115)
gui.Form1..minx(0)
gui.Form1..miny(0)
gui.Form1..position(0,0)
gui.Form1..event(unload,form1_unload)
gui.Form1..alwaysontop(False)
gui.Form1..fontname("Arial")
gui.Form1..fontsize(8)
gui.Form1..forecolor(0)
gui.Form1..fontstyle(,,,,)
gui.Form1..BackColor(-2147483633)
gui.Form1..controlbox(True)
gui.Form1..maxbutton(False)
gui.Form1..minbutton(True)
gui.Form1..mousepointer(0)
gui.Form1..moveable(True)
gui.Form1..sizeable(False)
gui.Form1..ShowInTaskBar(True)
gui.Form1..titlebar(True)
gui.Form1.txtFilePath.create(textbox,"",True,4620,300,0,200,400,False,0,Arial,8,-2147483643,1)
gui.Form1.txtFilePath.defaultvalue("")
gui.Form1.txtFilePath.controlgroup(0)
gui.Form1.lbl1.create(label,"Save File as:",True,1935,255,1,200,200,True,0,Arial,8,-2147483633,0)
gui.Form1.lbl1.defaultvalue("")
gui.Form1.lbl1.controlgroup(0)
gui.Form1.cmdBrwsFile.create(button)
gui.Form1.cmdBrwsFile.caption("^")
gui.Form1.cmdBrwsFile.visible(True)
gui.Form1.cmdBrwsFile.size(495,375)
gui.Form1.cmdBrwsFile.zorder(0)
gui.Form1.cmdBrwsFile.position(4900,370)
gui.Form1.cmdBrwsFile.enabled(True)
gui.Form1.cmdBrwsFile.fontname("Arial")
gui.Form1.cmdBrwsFile.fontsize(8)
gui.Form1.cmdBrwsFile.event(click,cmdbrwsfile_click)
gui.Form1.cmdBrwsFile.defaultvalue("")
gui.Form1.cmdBrwsFile.controlgroup(0)
gui.Form1.cmdOk.create(button)
gui.Form1.cmdOk.caption("Ok")
gui.Form1.cmdOk.visible(True)
gui.Form1.cmdOk.size(855,375)
gui.Form1.cmdOk.zorder(0)
gui.Form1.cmdOk.position(200,900)
gui.Form1.cmdOk.enabled(True)
gui.Form1.cmdOk.fontname("Arial")
gui.Form1.cmdOk.fontsize(8)
gui.Form1.cmdOk.event(click,cmdok_click)
gui.Form1.cmdOk.defaultvalue("")
gui.Form1.cmdOk.controlgroup(0)
gui.Form1.cmdCancel.create(button)
gui.Form1.cmdCancel.caption("Cancel")
gui.Form1.cmdCancel.visible(True)
gui.Form1.cmdCancel.size(855,375)
gui.Form1.cmdCancel.zorder(0)
gui.Form1.cmdCancel.position(1200,900)
gui.Form1.cmdCancel.enabled(True)
gui.Form1.cmdCancel.fontname("Arial")
gui.Form1.cmdCancel.fontsize(8)
gui.Form1.cmdCancel.event(click,form1_unload)
gui.Form1.cmdCancel.defaultvalue("")
gui.Form1.cmdCancel.controlgroup(0)
gui.Form1.cmdBrwsFile.tabstop(True)
gui.Form1.cmdBrwsFile.tabindex(1)
gui.Form1.cmdOk.tabstop(True)
gui.Form1.cmdOk.tabindex(2)
gui.Form1.cmdCancel.tabstop(True)
gui.Form1.cmdCancel.tabindex(3)
gui.Form1.txtFilePath.tabstop(True)
gui.Form1.txtFilePath.tabindex(4)


Program.Sub.ScreenSU.End

Program.Sub.Preflight.Start

Variable.Global.sRunTime.Declare(String)
Variable.Global.sRunCYMD.Declare(String)
Variable.Global.sFilePath.Declare(String)
Variable.UDT.uExport.Define("Dept",String)
Variable.UDT.uExport.Define("Reg_Hours",Float)
Variable.UDT.uExport.Define("OT",Float)
Variable.UDT.uExport.Define("Holiday",Float)
Variable.UDT.uExport.Define("PTO",Float)
Variable.UDT.uExport.Define("Bereavement",Float)
Variable.UDT.uExport.Define("Jury_Duty",Float)
Variable.UDT.uExport.Define("Employee",String,Employee)
Variable.UDT.uExport.Define("Name",String,Name)
Variable.UDT.uExport.Define("SSN",String,SSN)
Variable.UDT.uExport.Define("Pay_Type",String,Pay_Type)
Variable.UDT.uEmployee.Define("Employee",String,Employee)
Variable.UDT.uEmployee.Define("Name",String,Name)
Variable.UDT.uEmployee.Define("Type",String,Type)
Variable.UDT.uEmployee.Define("Hours",Float,Hours)
Variable.UDT.uEmployee.Define("Dept",String,Dept)
Variable.UDT.uEmployee.Define("EC",String,EC)
Variable.uGlobal.uEmployee.Declare("uEmployee")
Variable.uGlobal.uExport.Declare("uExport")
Program.Sub.Preflight.End

Program.Sub.Main.Start
'Coded by SFF
'1/5/2015
'Syndeo Bank Payroll Export

F.Intrinsic.Control.SetErrorHandler("Main_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)

V.Local.sRet.Declare(String)

'activate when delivered
F.Intrinsic.String.Left(V.Passed.888888,8,V.Global.sRunCYMD)
F.Intrinsic.String.Mid(V.Passed.888888,9,6,V.Global.sRunTime)


F.ODBC.Connection!conX.OpenConnection(V.Ambient.PDSN,V.Ambient.PUser,V.Ambient.PPass,72000)
Gui.Form1..Show
Gui.Form1.cmdBrwsFile.SetFocus

F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Label("Main_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
Function.Intrinsic.String.Concat("Project: New_Script.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
F.Intrinsic.UI.Msgbox(V.Local.sError)
Function.Intrinsic.Control.EndIf


Program.Sub.Main.End

program.sub.form1_unload.start
F.Intrinsic.Control.End

program.sub.form1_unload.end

program.sub.cmdok_click.start
F.Intrinsic.Control.SetErrorHandler("cmdok_click_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)
V.Local.sRet.Declare(String)
V.Local.sTemp.Declare(String)
V.Local.sSql.Declare(String)
V.Local.i.Declare(Float)
V.Local.iExp.Declare(Float)
V.Local.iRet.Declare(Float)

V.uGlobal.uEmployee.Redim(-1,-1)
V.uGlobal.uExport.Redim(-1,-1)

V.Global.sFilePath.Set(V.Screen.Form1!txtFilePath.Text)
F.Intrinsic.Control.If(V.Global.sFilePath.Trim,=,"")
	F.Intrinsic.UI.Msgbox("Select Save Location First")
	F.Intrinsic.Control.ExitSub
F.Intrinsic.Control.EndIf

Gui.Form1..Visible(false)
'load all records from PYRL_LABOR_XFER
F.Intrinsic.String.Build("select Employee,Name,Dept,EC,Type,Hours from PYRL_LABOR_XFER where RUN_CYMD = '{0}' and RUN_TIME = '{1}'",V.Global.sRunCYMD,V.Global.sRunTime,V.Local.sSql)
F.ODBC.Connection!conX.OpenRecordsetRO("rst",V.Local.sSql)
F.Intrinsic.Control.If(V.ODBC.conX!rst.EOF,<>,True)
	F.Intrinsic.Variable.LoadUDTFromRecordset("conX","rst","v.uGlobal.uEmployee",False)
F.Intrinsic.Control.EndIf
F.ODBC.conX!rst.Close

'LOAD Distinct Employee info to Export table first
F.Intrinsic.String.Build("Select Distinct Name,Employee,SOCIAL_SECURITY_NO as SSN,Pay_Type from V_EMPLOYEE_MSTR where Employee in (Select Employee from PYRL_LABOR_XFER where RUN_CYMD = '{0}' and RUN_TIME = '{1}')",V.Global.sRunCYMD,V.Global.sRunTime,V.Local.sSql)
F.ODBC.Connection!conX.OpenRecordsetRO("rst",V.Local.sSql)
F.Intrinsic.Control.If(V.ODBC.conX!rst.EOF,<>,True)
	F.Intrinsic.Variable.LoadUDTFromRecordset("conX","rst","v.uGlobal.uExport",False)
F.Intrinsic.Control.EndIf
F.ODBC.conX!rst.Close

'format uExport elements
F.Intrinsic.Control.For(V.Local.i,V.uGlobal.uExport.LBound,V.uGlobal.uExport.UBound,1)
F.Intrinsic.UI.InvokeWaitDialog("Creating Export File")
	'remove comma from name
	F.Intrinsic.String.Replace(V.uGlobal.uExport(v.Local.i)!Name,",","",V.Local.sRet)
	V.uGlobal.uExport(v.Local.i)!Name.Set(V.Local.sRet)
	'put dashes back in SSN
	F.Intrinsic.String.Left(V.uGlobal.uExport(v.Local.i)!SSN,3,V.Local.sRet)
	F.Intrinsic.String.Mid(V.uGlobal.uExport(v.Local.i)!SSN,4,2,V.Local.sTemp)
	F.Intrinsic.String.Build("{0}-{1}-",V.Local.sRet,V.Local.sTemp,V.Local.sRet)
	F.Intrinsic.String.Mid(V.uGlobal.uExport(v.Local.i)!SSN,6,4,V.Local.sTemp)
	F.Intrinsic.String.Concat(V.Local.sRet,V.Local.sTemp,V.Local.sRet)
	V.uGlobal.uExport(v.Local.i)!SSN.Set(V.Local.sRet)
F.Intrinsic.Control.Next(V.Local.i)

'Loop through uEmployee and on each record add value(ie. UEmployee!overtime hours + uExport!OT) and set back to uExport!OT
F.Intrinsic.Control.For(V.Local.i,V.uGlobal.uEmployee.LBound,V.uGlobal.uEmployee.UBound,1)
	F.Intrinsic.UI.ChangeWaitStatus("Distributing Hours",V.Local.i,V.uGlobal.uEmployee.LBound,V.uGlobal.uEmployee.UBound)
	F.Intrinsic.Variable.UDTMultiSeek(V.uGlobal.uExport!Employee,V.uGlobal.uEmployee(v.Local.i)!Employee,V.Local.sRet)
	F.Intrinsic.String.Split(V.Local.sRet,"*!*",V.Local.sRet)
	F.Intrinsic.Math.ConvertToFloat(V.Local.sRet(0),V.Local.iExp)
	'Regular
	F.Intrinsic.Control.If(V.uGlobal.uEmployee(v.Local.i)!Type,=,"RT")
		F.Intrinsic.Math.Add(V.uGlobal.uEmployee(V.Local.i)!Hours,V.uGlobal.uExport(v.Local.iExp)!Reg_Hours,V.Local.iRet)
		V.uGlobal.uExport(v.Local.iExp)!Reg_Hours.Set(V.Local.iRet)
	'Overtime
	F.Intrinsic.Control.Elseif(V.uGlobal.uEmployee(v.Local.i)!Type,=,"OT")
		F.Intrinsic.Math.Add(V.uGlobal.uEmployee(V.Local.i)!Hours,V.uGlobal.uExport(v.Local.iExp)!OT,V.Local.iRet)
		V.uGlobal.uExport(v.Local.iExp)!OT.Set(V.Local.iRet)
	'Holiday
	F.Intrinsic.Control.Elseif(V.uGlobal.uEmployee(v.Local.i)!Type,=,"HT")
		F.Intrinsic.Math.Add(V.uGlobal.uEmployee(V.Local.i)!Hours,V.uGlobal.uExport(v.Local.iExp)!Holiday,V.Local.iRet)
		V.uGlobal.uExport(v.Local.iExp)!Holiday.Set(V.Local.iRet)
	'PTO
	F.Intrinsic.Control.Elseif(V.uGlobal.uEmployee(v.Local.i)!EC,=,"04")
		F.Intrinsic.Math.Add(V.uGlobal.uEmployee(V.Local.i)!Hours,V.uGlobal.uExport(v.Local.iExp)!PTO,V.Local.iRet)
		V.uGlobal.uExport(v.Local.iExp)!PTO.Set(V.Local.iRet)
	'Bereavement
	F.Intrinsic.Control.Elseif(V.uGlobal.uEmployee(v.Local.i)!EC,=,"06")
		F.Intrinsic.Math.Add(V.uGlobal.uEmployee(V.Local.i)!Hours,V.uGlobal.uExport(v.Local.iExp)!Bereavement,V.Local.iRet)
		V.uGlobal.uExport(v.Local.iExp)!Bereavement.Set(V.Local.iRet)
	'jury duty
	F.Intrinsic.Control.Elseif(V.uGlobal.uEmployee(v.Local.i)!EC,=,"05")
		F.Intrinsic.Math.Add(V.uGlobal.uEmployee(V.Local.i)!Hours,V.uGlobal.uExport(v.Local.iExp)!Jury_Duty,V.Local.iRet)
		V.uGlobal.uExport(v.Local.iExp)!Jury_Duty.Set(V.Local.iRet)
	F.Intrinsic.Control.EndIf

F.Intrinsic.Control.Next(V.Local.i)

F.Intrinsic.Control.CallSub(Export)
F.Intrinsic.UI.CloseWaitDialog
'open file if wanted
F.Intrinsic.UI.Msgbox("Do you want to Open File?","Open File",4,V.Local.i)
F.Intrinsic.Control.If(V.Local.i,=,6)
	F.Intrinsic.Task.ShellExecSync(V.Caller.Handle,"OPEN",V.Global.sFilePath,"","",5)
F.Intrinsic.Control.Endif
F.Intrinsic.Control.CallSub(Form1_unload)


F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Label("cmdok_click_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: GCG_4054_Syndeo_Payroll_Export.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
Function.Intrinsic.Control.EndIf


program.sub.cmdok_click.end

program.sub.cmdbrwsfile_click.start
F.Intrinsic.Control.SetErrorHandler("cmdbrwsfile_click_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)
V.Local.sRet.Declare(String)
V.Local.sFile.Declare(String)

F.Intrinsic.String.Build("SyndeoPyrlExport{0}{1}",V.Global.sRunCYMD,V.Global.sRunTime,V.Local.sFile)
Function.Intrinsic.UI.ShowSaveFileDialog(V.Local.sFile,"Comma Separated File|.csv",,V.Local.sRet)
F.Intrinsic.Control.If(V.Local.sRet,<>,"***CANCEL***")
	Gui.Form1.txtFilePath.Text(V.Local.sRet)
F.Intrinsic.Control.EndIf


F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Label("cmdbrwsfile_click_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: GCG_4054_Syndeo_Payroll_Export.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
Function.Intrinsic.Control.EndIf



program.sub.cmdbrwsfile_click.end

Program.Sub.Export.Start
F.Intrinsic.Control.SetErrorHandler("Export_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)
V.Local.i.Declare(Float)
V.Local.iHandle.Declare(Float)
V.Local.sTemp.Declare(String)

'write line for column headers
V.Local.sTemp.Set("EMPL NO,EMPLOYEE NAME,SOCIAL SECURITY NUMBER,PAY_TYPE,REG HOURS,OVERTIME,HOLIDAY HOURS,PTO,BEREAVEMENT,JURY DUTY")
F.Intrinsic.File.String2File(V.Global.sFilePath,V.Local.sTemp)

'export file to .csv according to format for Syndeo
'each ordinal(v.local.i) is a line for the Export File
F.Intrinsic.Control.For(V.Local.i,V.uGlobal.uExport.LBound,V.uGlobal.uExport.UBound,1)
	F.Intrinsic.UI.ChangeWaitStatus("Writing to File",V.Local.i,V.uGlobal.uExport.LBound,V.uGlobal.uExport.UBound)
	F.Intrinsic.String.Build("{0},{1},{2},{3},{4},{5},{6},{7},{8},{9}",V.uGlobal.uExport(v.Local.i)!Employee,V.uGlobal.uExport(v.Local.i)!Name,V.uGlobal.uExport(v.Local.i)!SSN,V.uGlobal.uExport(v.Local.i)!Pay_Type,V.uGlobal.uExport(v.Local.i)!Reg_Hours,V.uGlobal.uExport(v.Local.i)!OT,V.uGlobal.uExport(v.Local.i)!Holiday,V.uGlobal.uExport(v.Local.i)!PTO,V.uGlobal.uExport(v.Local.i)!Bereavement,V.uGlobal.uExport(v.Local.i)!Jury_Duty,V.Local.sTemp)
	F.Intrinsic.File.Append2FileNewLine(V.Global.sFilePath,V.Local.sTemp)
F.Intrinsic.Control.Next(V.Local.i)


F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Label("Export_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: GCG_4054_Syndeo_Payroll_Export.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
Function.Intrinsic.Control.EndIf


Program.Sub.Export.End

Program.Sub.Comments.Start
${$0$}$Test Script$}$SFF$}$7/30/2014 10:28:13 AM
Program.Sub.Comments.End

